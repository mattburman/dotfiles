export DOTFILES=~/dotfiles
function .f() {
  cd "$DOTFILES/$1"
}
alias .z="source $DOTFILES/zsh/zshrc.zsh"

export DELTA_FEATURES=+navigate

function theme() {
  # todo change vim theme automatically
  if [[ ! ( $(darkMode) =~ 'Dark' ) ]]; then
    kitty +kitten themes --reload-in=all "Ayu Light"
  else
    kitty +kitten themes --reload-in=all "Ayu"
  fi
}

alias cd-="cd -"

# 1️⃣ Set up environment variables & PATH
export PATH=$PATH:.
export PATH=$PATH:$DOTFILES/zsh/path
export PATH="/opt/homebrew/bin:$PATH"
export PATH="/opt/homebrew/sbin:$PATH"
export FPATH="/opt/homebrew/share/zsh/site-functions:$FPATH"
export GOPATH=$HOME/go
export PATH=$PATH:$GOPATH/bin
export PATH="/usr/local/opt/libpq/bin:$PATH"
export PATH=$PATH:$DOTFILES/zsh/path

export TERM=xterm

# Enable prompt substitution and colors
setopt PROMPT_SUBST
autoload -U colors && colors

# 2️⃣ Load aliases/functions before initializing completion
for file (~/.zsh/aliases/*.*sh); do
  source $file
done

# 3️⃣ Initialize completion system
autoload -Uz compinit
if [ "$(date +'%j')" != "$(stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null || date +'%j')" ]; then
  compinit
else
  compinit -C
fi

# Load all completion files after compinit
for file (~/.zsh/completions/*.*sh); do
  source $file
done

# 4️⃣ Set `zstyle` configurations (must be after `compinit`)
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache
zstyle ':fzf-tab:complete:cd*:*' fzf-preview 'ls -l --color=always $realpath'
zstyle ':fzf-tab:*' switch-group ',' '.'
zstyle ':fzf-tab:*' fzf-flags --ansi --color=bg:#000000,fg:#ffffff,hl:#ff00ff,fg+:#00ff00,hl+:#ffff00 --bind=tab:accept
# zstyle ':fzf-tab:*' use-fzf-default-opts yes  # Uncomment if you need it

# 5️⃣ Load plugins (AFTER zstyle settings)
source ~/.zsh_plugins.sh  # Generated by dotbot install

# Load prompt configuration first
source ~/.zsh/lib/prompt.zsh

# Load all of the files in ZSH_PATH/lib that end in .*sh
for file (~/.zsh/lib/*.*sh); do
  [[ $file != *"prompt.zsh" ]] && source $file  # Skip prompt.zsh since we already loaded it
done

# 6️⃣ Load additional tools (fzf key bindings, kubectl aliases, etc.)
source ~/.zsh/submodules/fzf/shell/key-bindings.zsh
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey "^R" fzf-history-widget
bindkey "^T" fzf-file-widget
source ~/.zsh/submodules/kubectl-aliases/.kubectl_aliases

# 7️⃣ Load additional system-wide configurations
if test -e /etc/static/bashrc; then . /etc/static/bashrc; fi 2> /dev/null  # Import nix bashrc but ignore errors

# 8️⃣ Enable `kubectl` completion - lazy loaded
function kubectl() {
  unfunction "$0"
  if ! command -v kubectl &>/dev/null; then
    echo "kubectl not found"
    return 1
  fi
  source <(command kubectl completion zsh)
  command kubectl "$@"
}

ZSH_THEME_GIT_PROMPT_DIRTY="*"              # Text to display if the branch is dirty
ZSH_THEME_GIT_PROMPT_CLEAN=""               # Text to display if the branch is clean

alias vim="nvim"
alias v="nvim"
function .v() {
  v "$DOTFILES/$1"
}
function _DF {
  ((CURRENT == 2)) &&
  _files -/ -W $DOTFILES
}
compdef _DF .v

export EDITOR=vim
export VISUAL=vim
export SHELL=$(which zsh)

# slows down load, and dont use much. disable for now.
# export NVM_DIR="$HOME/.nvm"
# [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
# [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

uname | grep -q "Darwin" && source ~/.zsh/darwin.zsh

if test -e ~/.gvm/scripts/gvm; then . "$HOME"/.gvm/scripts/gvm; fi
if test -e ~/.cargo/env; then . ~/.cargo/env; fi

export XDG_DATA_DIRS=$HOME/.nix-profile/share:$XDG_DATA_DIRS

if test -e ~/.direnvrc; then eval "$(direnv hook zsh)"; fi

export GPG_TTY=$(tty)

export PATH="/opt/homebrew/opt/mysql@8.4/bin:$PATH"

# Ignore non-existent commands and paths
zstyle ':completion:*:warnings' format ''
